buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://dl.google.com/dl/android/maven2'
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'
    }
}

apply plugin: 'com.android.application'

allprojects {
    repositories {
        jcenter()
        maven {
            url 'https://dl.google.com/dl/android/maven2'
        }
    }
}

def isIde = { ->
    def ide = false
    def taskRequests = project.gradle.startParameter.taskRequests
    if (taskRequests?.empty) {
        ide = true
    } else {
        taskRequests[0].args.any {
            if (it.endsWith('generateDebugSources') || it.endsWith('assembleDebug')) {
                ide = true
                return true
            }
        }
        return ide
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.0"

    defaultConfig {
        versionName new XmlSlurper().parse("AndroidManifest.xml").@'android:versionName'.text()
        buildConfigField "boolean", "RELEASE", "false"
        buildConfigField "String", "DONATE_ALIPAY", "null"
        buildConfigField "String", "DONATE_PAYPAL", "null"
        buildConfigField "String", "EMAIL", "null"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            if (isIde()) {
                java.srcDirs = ['src', 'services/core/java', 'AndroidHiddenAPI']
            } else {
                java.srcDirs = ['src', 'services/core/java']
            }
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        androidTest.setRoot('tests')
    }

    if (new File("ant.properties").exists()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(file("ant.properties")))

        signingConfigs {
            release {
                storeFile file(properties['key.store'])
                storePassword properties['key.store.password']
                keyAlias properties['key.alias']
                keyPassword properties['key.alias.password']
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
            debug {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
}

dependencies {
    if (!isIde()) {
        compileOnly fileTree(dir: 'lib', include: ['*.jar'])
    }
    implementation "com.android.support:support-core-ui:27.0.2"
    implementation "com.android.support:support-v13:27.0.2"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
    if (!isIde()) {
        options.compilerArgs << '-Xbootclasspath/p:' + fileTree(dir: 'lib', include: ['*.jar']).asPath
    }
}
